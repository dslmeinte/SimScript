/*
 * A language to bridge queries and structures by defining mappings between them.
 * (Could even be used to define complete services implementations.)
 */

grammar nl.dslmeinte.simscript.mappings.MappingsDsl with nl.dslmeinte.simscript.structure.SimStructureDsl

generate mappingsDsl "http://www.dslconsultancy.com/simscript/mappings/MappingsDsl"

import "http://www.dslconsultancy.com/simscript/simpleDb/SimpleDbDsl" as db
import "http://www.dslconsultancy.com/simscript/structure/StructureDsl" as struct


MappingsModel:
	('backend' '=' backendTarget=BackendTargets)?	// default: PHP
	definitions+=NamedQueryDefinition*
	;

enum BackendTargets:
	php='PHP' /* default */ | java='Java'
	;


/*
 * +---------+
 * | queries |
 * +---------+
 */

NamedQueryDefinition:
	'query' name=ID ':'
		queryExpr=Query
	;

Query:
	FromDbQuery | ToDbQuery
	;
	/*
	 * pulled-up features:
	 * 		name	: ID
	 * 		table	: db::Table
	 */

FromDbQuery:
	QueryHavingResult | DeleteQuery
	;
	/*
	 * pulled-up features:
	 * 		parameters	: UntypedParameters*
	 * 		whereClause	: MatchingClause
	 */

ToDbQuery:
	InsertQuery | UpdateQuery
	;

QueryHavingResult:
	SelectQuery | CountQuery 
	;

SelectQuery:
	('with' '(' parameters+=UntypedParameter* ')')?		// (also in CountQuery and DeleteQuery)
	'select' nonEmpty?='non-empty'?
	'from' table=[db::Table]							// (also in CountQuery and DeleteQuery)
	whereClause=MatchingClause?							// (also in CountQuery and DeleteQuery)
	orderClause=OrderClause?
	atMost1?='at-most-1'?
	// mapping part:
	'map-to' structure=[struct::Structure] '{'
		( 'same-named' '[' sameNamedColumns+=[db::Column]* ']' )?
		mappingSpecifications+=FromDbMappingSpecification*
	'}'
	;
	/*
	 * scoping:
	 * 		1-  sameNamedColumns in table.columns s.t. a same-named feature of structure exists	[√]
	 * validation:
	 * 		1-  warning if resultExpr yields (at most) single row && atMost1					[√]
	 * 		2-  warning for unmapped-/-deferred features										[√]
	 * 		3-	error for duplicately-named columns under same-named							[ ]
	 * 
	 * semantics:
	 * 		nonEmpty expresses that the #result-rows is checked to be > 0
	 * 		atMost1 expresses that at most 1 result row is returned/used
	 */

FromDbMappingSpecification:
	(
		  {SimpleColumnMappingSpecification}	column=[db::Column] '->'
		| {NestedMappingSpecification}			query=[NamedQueryDefinition] '(' arguments+=Argument* ')' '->'
		| {DeferedMappingSpecification}			'defer'
		| {TimestampColumnMappingSpecification}	'timestamp' '->'
	)
		feature=[struct::Feature]
	;
	/*
	 * scoping:
	 * 		1-  feature in containing(SelectQuery).structure.features												[√]
	 * 		2-  <SimpleColumnMappingSpecification> column in containing(SelectQuery).table.columns					[?]		FIXME
	 * 
	 * validation:
	 * 		1-  <NestedMappingSpecification> query.queryExpr is-a QueryHavingResult									[√]
	 * 		2-  <NestedMappingSpecification> warning if query not in containing(MappingsModel)						[√]
	 * 		3-  <TimestampColumnMappingSpecification> only valid if containing(QueryHavingResult).table.timestamped	[√]
	 * 
	 * derived function:
	 * 		nestedQuery(NestedMappingSpecification) := it.query.queryExpr as QueryHavingResult						[√]
	 */

Argument:
	parameter=[UntypedParameter] '=' column=[db::Column]
	;
	/*
	 * scoping:
	 * 		1-  parameter in containing(NestedMappingSpecification).selectQuery.parameters	[√]
	 * 		2-  column in containing(SelectQuery).table.columns								[√]
	 */


CountQuery:
	('with' '(' parameters+=UntypedParameter* ')')?
	'count'
		resultExpr=ResultExpression
	'from' table=[db::Table]
	whereClause=MatchingClause?
	;
	// validation: warning if resultExpr yields (at most) single row	[√]

ResultExpression:
	ColumnsSpecification | AllColumnsExpression
	;

ColumnsSpecification:
	'[' columns+=[db::Column]+ ']'
	;
	/*
	 * scoping:		columns in containing(FromDbQuery).table.columns	[√]
	 * validation:	!columns.empty (warning)							[√]
	 */

AllColumnsExpression:
	{AllColumnsExpression} '*'
	;

MatchingClause:
	{MatchingClause}
	'matching' '(' subClauses+=MatchingSubClause* ')'
	;
	// validation: !subClauses.empty 								[√]
	// TODO  consider adding an "auto-matcher" based on column and parameter names

MatchingSubClause:
	SimpleSubClause | OrSubClause
	;

SimpleSubClause:
	SimpleEqualitySubClause | SimpleRangeSubClause
	;
	/*
	 * scoping: column in containing(FromDbQuery).table.columns		[√]
	 * pulled-up feature: column : db::Column
	 */

SimpleEqualitySubClause:
	column=[db::Column] '=' ( parameter=[UntypedParameter] | literal=LiteralExpression )
	;
	// scoping: parameter in containing(FromDbQuery).parameters		[√]
	// TODO  use a proper expression sub language for the rhs

SimpleRangeSubClause:
	column=[db::Column] 'in' leftDelimiter=LeftRangeDelimiters leftParameter=[UntypedParameter] ',' rightParameter=[UntypedParameter] rightDelimiter=RightRangeDelimiters
	;
	// scoping: leftParameter, rightParameter in containing(FromDbQuery).parameters		[√]
	// TODO  use a proper expression sub language for the rhs parameters

OrSubClause:
	'(' subClauses+=SimpleSubClause ( '||' subClauses+=SimpleSubClause )+ ')'
	;

enum LeftRangeDelimiters:
	closed='[' | open='<'
	;

enum RightRangeDelimiters:
	closed=']' | open='>'
	;


OrderClause:
	{OrderClause}
	'order-by'
		(
			  columnSpecifications+=ColumnOrderSpecification
			| '[' columnSpecifications+=ColumnOrderSpecification* ']'
		)
	;
	// validation: !columnSpecifications.empty (warning)			[√]

ColumnOrderSpecification:
	( column=[db::Column] | timestamp?='timestamp' ) direction=OrderDirections?
	;
	/*
	 * scoping:		column in containing(SelectQuery).table									[√]
	 * validation:	timestamp can only be used in a query whose table is timestamped		[√]
	 */

enum OrderDirections:
	asc='ascending' /* default */ | desc='descending'
	;


InsertQuery:
	'insert' '(' parameters+=TypedParameter (',' parameters+=TypedParameter )* ')' 'into' table=[db::Table] '{'
		mappingSpecifications+=ToDbMappingSpecification*
	'}' ( '=>' returnAutoIncrementColumn=[db::Column] )?
	;
	// scoping: returnAutoIncrementColumn in table.columns.filter[it.autoIncrement]		[√]

ToDbMappingSpecification:
	column=[db::Column] '<-' expr=ToDbExpression
	;
	/*
	 * scoping: column in containing(ToDbQuery).table.columns					[√]
	 * validation: passwords have to be encrypted								[√]
	 * 		(TODO  this type of stuff should be part of a static type system (checker))
	 */


UpdateQuery:
	'with' '(' parameters+=TypedParameter (',' parameters+=TypedParameter )* ')'
	'update' table=[db::Table]
	whereClause=UpdateMatchingClause
	'{' mappingSpecifications+=ToDbMappingSpecification* '}'
	;

UpdateMatchingClause:
	{UpdateMatchingClause}
	'matching' '(' subClauses+=ExpressionEqualitySubClause* ')'
	;
	// validation: !subClauses.empty 											[√]

ExpressionEqualitySubClause:
	column=[db::Column] '=' expr=ToDbExpression
	;
	// scoping: columns in containing(UpdateQuery).table.columns				[√]
	// validation: !column.primary (warning)									[√]


DeleteQuery:
	('with' '(' parameters+=UntypedParameter* ')')?
	'delete' 'from' table=[db::Table]
	whereClause=MatchingClause
	;


/*
 * +-------------+
 * | expressions |
 * +-------------+
 */

ToDbExpression:
	  FeatureExpressionHead
	| MonthRoundExpression
	| EncryptExpression
	| LiteralExpression
	// TODO  add other expressions (literals and such)
	;

MonthRoundExpression:
	'round-to-month' '(' operand=FeatureExpressionHead ')'
	;
	// validation: operand must be date-typed									[√]

EncryptExpression:
	'encrypt' '(' operand=FeatureExpressionHead ')'
	;
	// validation: operand must be password-typed								[√]


// TODO  turn into combination of FeatureExpression + ReferenceExpression, as in SimScript (requires proper scoping)

FeatureExpressionHead:
	parameter=[TypedParameter] tail=FeatureExpressionTail?
	;
	// scoping: parameter in containing(ToDbQuery).parameters					[√]

FeatureExpressionTail:
	'.' feature=[struct::Feature] tail=FeatureExpressionTail?
	;
	// scoping: feature in (eContainer as FeatureExpressionElement).features	[√]

FeatureExpressionElement:	// convenience type with 'tail' features
	FeatureExpressionHead | FeatureExpressionTail
	;
	/*
	 * pulled-up feature:
	 * 		tail	: FeatureExpressionTail
	 */

LiteralExpression:
	{BooleanLiteralExpression} literal=BooleanLiterals
	;

enum BooleanLiterals:
	false /* == ord(0) */ | true /* == order(1) */
	;


/*
 * +------------+
 * | referables |
 * +------------+
 */

TypedParameter:
	name=ID ':' type=TypeLiteral
	;

UntypedParameter:
	name=ID		// (no type info yet, as this isn't currently really relevant for the PHP code...)
	;

